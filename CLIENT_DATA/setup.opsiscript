[Actions]

include_insert "common.opsiinc"

if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
	LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
	isFatalError
	; Stop process and set installation status to failed
else
	if FileExists("%ScriptPath%\delsub.opsiscript")
		comment "Start uninstall sub section"
		Sub "%ScriptPath%\delsub.opsiscript"
	endif

	Message "Installing %installingProdName% (%installingProdVersion%) " + $Bit$ +" Bit..."

	comment "Start setup program"
	Set $registerMso$ = getProductProperty("register-mso","")
	if ( $registerMso$ = "no")
		Set $registerMso$ = "NO"
	else
		Set $registerMso$ = "ALL"
	endif
	
	comment "Installing the main Program for LibreOffice"
	Winbatch_install_main
	Set $ExitCode$ = getLastExitCode
	if stringToBool(isMsExitcodeFatal($ExitCode$, "True", $ErrorString$))
		isFatalError
	endif
	
	comment "Installing the Help-Pack for LibreOffice"
	Winbatch_install_help
	Set $ExitCode$ = getLastExitCode
	if stringToBool(isMsExitcodeFatal($ExitCode$, "True", $ErrorString$))
		isFatalError
	endif
	
	comment "Test for installation success"

	; Ergebnis ist StringList (mehrere Einträge möglich);
	; in einer Zeile werden drei Ergebnisse mit "|" abgetrennt geliefert: Registrykey (mit Pfad) | Bereich Registry (kann direkt getRegistryValue übergeben werden | DisplayName
	; z.B. HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\Test64|64bit|Test Registry Entry64
	Set $ResultList$ = SearchRegistryUninstallKeys($SearchPattern$)

	if ( count ($ResultList$) = "0" )
		logError "Fatal: After Installation " + $ProductName$ + " could not be found in registry"
		isFatalError
	else
		; gibt zwei Einträge (Programm & Hilfe), aber egal, in beiden ist InstallLocation bzw. DisplayVersion enthalten
		Set $RegId$ = takeString (0,splitString(takeString(0,$ResultList$), '|'))
		Set $RegBitScope$ = takeString (1,splitString(takeString(0,$ResultList$), '|'))

		comment "Check version"
		Set $InstalledVersion$ = GetRegistryValue($RegId$, "DisplayVersion", $RegBitScope$)
		if not (contains($InstalledVersion$, $ProductVersion$))
			LogError "Wrong version number found, something went wrong."
			isFatalError
		endif

		comment "Get 'InstallLocation' from registry for path to exe"
		Set $InstallDir$ = GetRegistryValue($RegId$, "InstallLocation", $RegBitScope$)
		Set $PathToExe$ = $InstallDir$ + $Launcher$
		Set $PathToUnopkg$ = $InstallDir$ + "\program\unopkg.exe"
		
		if $DesktopLink$ = "true"
			comment "Create common desktop link"
			Linkfolder_create_desktoplink
		endif
		
		comment "include custom post install file"
		set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
		if not ($CustomPostInstall$ = "none")
			if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
				include_insert "%ScriptPath%\custom\" + $CustomPostInstall$	
			endif
		endif
		
		comment "Install Extensions if enabled and available"
		set $CustomExtensions$ = getProductProperty("custom-extensions","no")
		if ($CustomExtensions$ = "yes")
			if FileExists ($OxtDir$)
				Set $OxtList$ =  getOutStreamFromSection ("execWith_dir_oxt '%System%\cmd.exe' /C")
				if ( count ($OxtList$) = "0")
					Logwarning "No extensions (*.oxt) found for installation in $OxtDir$"
				else
					for %o% in $OxtList$ do Winbatch_install_oxt
				endif
			else
				LogWarning "Directory $OxtDir$ not found! Unable to install extensions!" 
			endif
		endif
	endif
endif

;$registerMso$ gibt an, ob MSO-Dateien automatisch erkannt werden sollen
[Winbatch_install_main]
msiexec $SilentOption$ /i "%ScriptPath%\$MsiFile$" /l* "$LogDir$\$MsiFile$.install_log.txt" UI_LANGS=$UILangs$ REGISTER_$registerMso$_MSO_TYPES=1 ISCHECKFORPRODUCTUPDATES=0 CREATEDESKTOPLINK=0 RebootYesNo=No ADDLOCAL=ALL REMOVE=gm_o_Onlineupdate

[Winbatch_install_help]
msiexec $SilentOption$ /i "%ScriptPath%\$MsiFileHelpPack$" /l* "$LogDir$\$MsiFileHelpPack$.install_log.txt"

[execWith_dir_oxt]
@echo off
dir /b "$OxtDir$\*.oxt" 2> null

[Winbatch_install_oxt]
"$PathToUnopkg$" add --suppress-license --shared "$OxtDir$\%o%"

[Linkfolder_create_desktoplink]
set_basefolder common_desktopdirectory
set_subfolder ""

set_link
 	name: $ProductName$
 	target: $PathToExe$
end_link
