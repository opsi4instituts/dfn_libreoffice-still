; Copyright (c) uib gmbh (www.uib.de)
; This sourcecode is owned by uib
; and published under the Terms of the General Public License.
; credits: http://www.opsi.org/credits/

[Actions]

include_insert "common.opsiinc"

if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
	LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
	isFatalError
	; Stop process and set installation status to failed
else
	Message "Installing %installingProdName% (%installingProdVersion%) 64 Bit..."	
	
	comment "Show product picture"
	ShowBitmap "%ScriptPath%\" + $ProductId$ + ".png" $ProductName$
	
	if FileExists("%ScriptPath%\delsub.opsiscript")
		comment "Start uninstall sub section"
		Sub "%ScriptPath%\delsub.opsiscript"
	endif
	
	comment "Start setup program"
	Set $registerMso$ = getProductProperty("register-mso","")
	if ( $registerMso$ = "no")
		Set $registerMso$ = "NO"
	else
		Set $registerMso$ = "ALL"
	endif
	
	comment "Installing the main Program for LibreOffice"
	Winbatch_install_main
	sub_check_exitcode
	
	comment "Installing the Help-Pack for LibreOffice"
	Winbatch_install_help
	sub_check_exitcode
	
	comment "Test for installation success"
	
	; Parameter: $SearchPattern$: Suchbegriff in Registry
	Sub_search_registry64_uninstall_keys
	; Rückgabewert: $ResultList64$ gefundene Einträge
	if ( count ($ResultList64$) = "0" )
		logError "Fatal: After Installation " + $ProductName$ + " could not be found in " + $RegPathUninstall$
		isFatalError
	else
		Set $RegId$ = takeString(0, $ResultList64$)
		comment "Get 'InstallLocation' from registry for path to exe"
		Set $InstallDir$ = GetRegistryStringValue64("[" + $RegPathUninstall$ + "\" + $RegId$ + "] InstallLocation")
		Set $PathToExe$ = $InstallDir$ + $Launcher$
		Set $PathToUnopkg$ = $InstallDir$ + "\program\unopkg.exe"
		
		set $DesktopIcon$ = getProductProperty("desktopicon","no")
		if $DesktopIcon$ = "yes"
			comment "Create common desktop icon"
			Linkfolder_install_desktopicon
		endif
		
		comment "include custom post install file"
		set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
		if not ($CustomPostInstall$ = "none")
			if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
				include_insert "%ScriptPath%\custom\" + $CustomPostInstall$	
			endif
		endif
		
		comment "Install Extensions if enabled and available"
		set $CustomExtensions$ = getProductProperty("custom-extensions","no")
		if ($CustomExtensions$ = "yes")
			if FileExists ($OxtDir$)
				Set $OxtList$ =  getOutStreamFromSection ("execWith_dir_oxt '%System%\cmd.exe' /C")
				if ( count ($OxtList$) = "0")
					Logwarning "No extensions (*.oxt) found for installation in $OxtDir$"
				else
					for %o% in $OxtList$ do Winbatch_install_oxt
				endif
			else
				LogWarning "Directory $OxtDir$ not found! Unable to install extensions!" 
			endif
		endif
	endif
endif

;$registerMso$ gibt an, ob MSO-Dateien automatisch erkannt werden sollen
[Winbatch_install_main]
msiexec $MsiSilentOption$ /i "%ScriptPath%\$MsiFile$" /l* "$LogDir$\$MsiFile$.install_log.txt" UI_LANGS=$UILangs$ REGISTER_$registerMso$_MSO_TYPES=1 ISCHECKFORPRODUCTUPDATES=0 CREATEDESKTOPLINK=0 RebootYesNo=No ADDLOCAL=ALL REMOVE=gm_o_Onlineupdate

[Winbatch_install_help]
msiexec $MsiSilentOption$ /i "%ScriptPath%\$MsiFileHelpPack$" /l* "$LogDir$\$MsiFileHelpPack$.install_log.txt"

[execWith_dir_oxt]
@echo off
dir /b "$OxtDir$\*.oxt" 2> null

[Winbatch_install_oxt]
"$PathToUnopkg$" add --suppress-license --shared "$OxtDir$\%o%"

[Linkfolder_install_desktopicon]
set_basefolder common_desktopdirectory
set_subfolder ""

set_link
 	name: $ProductName$
 	target: $PathToExe$
end_link
