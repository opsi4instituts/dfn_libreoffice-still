; Parameter: $SearchPattern$
; separate ResultList32 bzw. 64 damit später die InstallDir aus dem richtigen Registryzweig geholt werden kann
Sub_search_registry64_uninstall_keys
Sub_search_registry32_uninstall_keys
; Rückgabewert: $ResultList$ gefundene Einträge

if ( count ( $ResultList32$ ) = "0" AND count( $ResultList64$ ) = "0" )
	comment "No installations of " + $SearchPattern$ + " found in registry"
else
	comment "Found at least one installation of " + $SearchPattern$ + " in registry, try to deinstall"

	; mehr als ein Paket zu deinstallieren (Programm und Hilfe haben separate Registryeinträge)
	comment "Uninstalling found programs"
	if not ( count ( $ResultList32$ ) = "0" )
		for %s% in $ResultList32$ do Sub_uninstall32
	endif
	if not ( count ( $ResultList64$ ) = "0"  )
		for %s% in $ResultList64$ do Sub_uninstall64
	endif
	
	if $DesktopLink$ = "true"
		comment "Delete common desktop link"
		Linkfolder_delete_desktoplink
	endif

	comment "include custom post install file"
	set $CustomPostUninstall$ = getProductProperty("custom-post-deinstall","none")
	if not ($CustomPostUninstall$ = "none")
		if FileExists("%ScriptPath%\custom\" + $CustomPostUninstall$)
			include_insert "%ScriptPath%\custom\" + $CustomPostUninstall$
		endif
	endif
endif

[Sub_uninstall32]
Set $InstallDir$ =  GetRegistryStringValue32("[" + $RegPathUninstall$ + "\%s%] InstallLocation")
Set $RegId$ = "%s%"

Winbatch_uninstall
sub_check_exitcode

; evtl. noch vorhandende Reste des Installationsverzeichnisses löschen
if FileExists ( $InstallDir$ )
	Files_uninstall
endif

[Sub_uninstall64]
Set $InstallDir$ =  GetRegistryStringValue64("[" + $RegPathUninstall$ + "\%s%] InstallLocation")
Set $RegId$ = "%s%"

Winbatch_uninstall
sub_check_exitcode

; evtl. noch vorhandende Reste des Installationsverzeichnisses löschen
if FileExists ( $InstallDir$ )
	Files_uninstall
endif

[Winbatch_uninstall]
msiexec /x $RegId$ $MsiSilentOption$ REBOOT=ReallySuppress

[Files_uninstall]
delete -sf "$InstallDir$"

[Linkfolder_delete_desktoplink]
set_basefolder common_desktopdirectory
set_subfolder ""
delete_element $ProductName$

