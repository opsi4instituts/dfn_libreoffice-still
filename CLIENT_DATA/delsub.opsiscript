; Ergebnis ist StringList (mehrere Einträge möglich);
; in einer Zeile werden drei Ergebnisse mit "|" abgetrennt geliefert: Registrykey (mit Pfad) | Bereich Registry (kann direkt getRegistryValue übergeben werden | DisplayName
; z.B. HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\Test64|64bit|Test Registry Entry64
Set $ResultList$ = SearchRegistryUninstallKeys($SearchPattern$)

if ( count ( $ResultList$ ) = "0" )
	comment "No installations of " + $SearchPattern$ + " found in registry"
else
	comment "Found at least one installation of " + $SearchPattern$ + " in registry, try to deinstall"

	; mehr als ein Paket zu deinstallieren (Programm und Hilfe haben separate Registryeinträge)
	comment "Uninstalling found programs"
	for %r% in $ResultList$ do Sub_uninstall
	
	if $DesktopLink$ = "true"
		comment "Delete common desktop link"
		Linkfolder_delete_desktoplink
	endif

	comment "include custom post install file"
	set $CustomPostUninstall$ = getProductProperty("custom-post-deinstall","none")
	if not ($CustomPostUninstall$ = "none")
		if FileExists("%ScriptPath%\custom\" + $CustomPostUninstall$)
			include_insert "%ScriptPath%\custom\" + $CustomPostUninstall$
		endif
	endif
endif

[Sub_uninstall]
Set $RegId$ = takeString (0,splitString("%r%", '|'))
Set $RegBitScope$ = takeString (1,splitString("%r%", '|'))
Set $InstallDir$ = GetRegistryValue($RegId$, "InstallLocation", $RegBitScope$)
; bei MSI wird nur der RegKey ohne Pfad benötigt
Set $RegId$ = takeString (-1,splitString($RegId$,"\"))

Winbatch_uninstall
Set $ExitCode$ = getLastExitCode
if StringToBool(isMsiExitcodeFatal($ExitCode$, "True", $ErrorString$))
	isFatalError
endif

; evtl. noch vorhandende Reste des Installationsverzeichnisses löschen
if FileExists ( $InstallDir$ )
	Files_uninstall
endif

[Winbatch_uninstall]
msiexec /x $RegId$ $SilentOption$ REBOOT=ReallySuppress

[Files_uninstall]
delete -sf "$InstallDir$"

[Linkfolder_delete_desktoplink]
set_basefolder common_desktopdirectory
set_subfolder ""
delete_element $ProductName$

